ws = _{ SPACE_SEPARATOR | "\t" }

markdown = { SOI ~ (block ~ empty_line*)* ~ EOI }

empty_line = {
	NEWLINE
}

block = _{
  heading
  | quote
  | code_block
  | horizontal_rule
  | paragraph
}

horizontal_rule = {
    ("---"|"***"|"–––") ~ ws* ~ (NEWLINE | EOI)
}

code_block = {
    "```" ~ (code_lang ~ ws* ~ NEWLINE)? ~ code_content ~ NEWLINE? ~ "```"
}

code_lang = {
    ws* ~ ('a'..'z' | 'A'..'Z')+
}

code_content = {
    (!(NEWLINE? ~ "```") ~ ANY)+
}

quote =  {
    ">" ~ paragraph
}

paragraph = {
	paragraph_line+ 
}

paragraph_line = {
	text+ ~ paragraph_break?
}

paragraph_break = _{
    NEWLINE
}

text = _{
    plain_text
  | escaped
  | styled_text
}

styled_text = _{
    escaped* ~ (bold | underline | italic | strikethrough | inline_image | inline_link | content) ~ escaped*
}

inline_image = {
	"![" ~ alt_text ~ "](" ~ url ~ ")"
}

inline_link = {
	"[" ~ link_text ~ "](" ~ url ~ ")"
}

link_text = {
	(!"]" ~ ANY)+
}

alt_text = {
	(!"]" ~ ANY)+
}

url = {
	(!")" ~ ANY)+
}

strikethrough = {
    "~~" ~ (styled_text)+ ~ "~~"
}

underline = {
    "__" ~ (styled_text)+ ~ "__"
}

bold = {
    "**" ~ (styled_text)+ ~ "**"
}

italic = {
    ("*" ~ (styled_text)+ ~ "*")
  | ("_" ~ (styled_text)+ ~ "_")
}

content = @{
    (!(exclude_styles | exclude_block_elems) ~ ANY)+
}

escaped = {
    "\\" ~ (!ws ~ char)
}

plain_text = @{
    !exclude_block_elems ~ (!exclude_styles ~ ANY)+
}

char = {
	ANY
}

exclude_styles = _{
  NEWLINE
  | escaped
  | "*"
  | "~~"
  | "_"
  | inline_link
  | inline_image
}

exclude_block_elems = _{
  heading
  | quote
  | code_block
  | horizontal_rule
}

heading = _{
    heading1
  | heading2
  | heading3
}

heading1 = {
    "#" ~ ws ~ single_line_text ~ NEWLINE?
}

heading2 = {
    "##" ~ ws ~ single_line_text ~ NEWLINE? 
}

heading3 = {
    "###" ~ ws ~ single_line_text ~ NEWLINE?
}

single_line_text = {
    (!NEWLINE ~ ANY)+
}